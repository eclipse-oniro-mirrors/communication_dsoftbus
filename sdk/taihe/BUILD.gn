# Copyright (c) 2026 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
 
import("../../dsoftbus.gni")
import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")

config("taihe_config") {
  include_dirs = [
    "${dsoftbus_root_path}/interfaces/kits/connection",
    "${dsoftbus_root_path}/sdk/taihe/interface",
    "${dsoftbus_root_path}/sdk/napi/link_enhance/include",
  ]
}

subsystem_name = "communication"
part_name = "dsoftbus"
taihe_generated_file_path = "$taihe_file_path/out/$subsystem_name/$part_name/sdk"

copy_taihe_idl("copy_linkEnhance") {
  sources = ["idl/ohos.distributedsched.linkEnhance.taihe"]
}

ohos_taihe("run_linkEnhance_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [":copy_linkEnhance"]
  outputs = [
    "$taihe_generated_file_path/src/ohos.distributedsched.linkEnhance.ani.cpp",
    "$taihe_generated_file_path/src/ohos.distributedsched.linkEnhance.abi.c",
  ]
}

taihe_shared_library("linkEnhance_taihe_native") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  sources = get_target_outputs(":run_linkEnhance_taihe")
  public_configs = [ ":taihe_config" ]
  sources += [
    "src/ani_constructor.cpp",
    "src/ohos.distributedsched.linkEnhance.impl.cpp",
    "src/link_enhance_utils_taihe.cpp"
  ]

  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  deps = [
    ":run_linkEnhance_taihe",
    "${dsoftbus_dfx_path}:softbus_dfx",
    "${dsoftbus_root_path}/adapter:softbus_adapter",
    "${dsoftbus_root_path}/core/common:softbus_utils",
    "${dsoftbus_root_path}/sdk:softbus_client",
    "${dsoftbus_dfx_path}/dumper/legacy:softbus_dfx_dump",
  ]
  external_deps = [
    "c_utils:utils",
    "bounds_checking_function:libsec_shared",
    "hilog:libhilog",
    "napi:ace_napi",
  ]
}

generate_static_abc("linkEnhance_taihe_ani") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.distributedsched.linkEnhance.ets" ]
  is_boot_abc = "True"
  device_dst_file = "system/framework/linkEnhance_taihe_ani.abc"
  dependencies = [ ":run_linkEnhance_taihe" ]
}

ohos_prebuilt_etc("linkEnhance_abc_etc") {
  source = "$target_out_dir/linkEnhance_taihe_ani.abc"
  module_install_dir = "framework"
  subsystem_name = "$subsystem_name"
  part_name = "$part_name"
  deps = [ ":linkEnhance_taihe_ani" ]
}

group("linkEnhance_taihe") {
  deps = [
      ":linkEnhance_abc_etc",
      ":linkEnhance_taihe_native",
  ]
}